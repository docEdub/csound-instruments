/*
 *  source-01.orc
 *
 *  Audio source module with two oscillators and a sub-oscillator.
 *
 *  The `poscil3` opcode is used for sine waves and the `vco2` opcode is used for other waveforms.
 */

/// @internal
opcode _source_01_module_audio_generator, a, kkiV
    kAmp, kCps, iMode, kPulseWidth xin

    if (iMode == {{source_01.wave.Sine}}) then
        aOut = poscil3(kAmp, kCps)
    else
        iVco2Mode = \
            iMode == {{source_01.wave.Saw}} ? {{vco2.mode.Sawtooth}} : \
            iMode == {{source_01.wave.Pulse}} ? {{vco2.mode.Square}} : \
            iMode == {{source_01.wave.Triangle}} ? {{vco2.mode.SawtoothTriangleRamp}} : \
            -1
        aOut = vco2(kAmp, kCps, iVco2Mode, kPulseWidth)
        ; {{LogDebug_k '("kAmp = %f", kAmp)'}}
        ; {{LogDebug_k '("kNoteNumber_1 = %f", kNoteNumber_1)'}}
        ; {{LogDebug_i '("iVco2Mode = %f", iVco2Mode)'}}
        ; {{LogDebug_k '("kPulseWidth = %f", kPulseWidth)'}}
    endif

    xout(aOut)
endop


/// Generates audio using two oscillators and a sub-oscillator.
/// The `poscil3` opcode is used for sine waves and the `vco2` opcode is used for other waveforms.
/// @param 1 Channel prefix used for the host automation parameters.
/// @out Mono audio signal generated by the oscillators.
///
opcode AF_source_01_module, a, S
    SChannelPrefix xin
    aOut = 0

    if ({{getHostValue}}:k(strcat(SChannelPrefix, "_enabled")) == {{false}}) then
        kgoto end
    endif

    kHost_main_amp     = {{getHostValue}}:k(strcat(SChannelPrefix, "_main_amp"))

    kHost_enabled_1     = {{getHostValue}}:k(strcat(SChannelPrefix, "_enabled_1"))
    iHost_wave_1        = {{getHostValue}}:i(strcat(SChannelPrefix, "_wave_1"))
    kHost_pulseWidth_1  = {{getHostValue}}:k(strcat(SChannelPrefix, "_pulseWidth_1"))
    kHost_fine_1        = {{getHostValue}}:k(strcat(SChannelPrefix, "_fine_1"))

    kHost_mix           = {{getHostValue}}:k(strcat(SChannelPrefix, "_mix"))

    kHost_enabled_2     = {{getHostValue}}:k(strcat(SChannelPrefix, "_enabled_2"))
    iHost_wave_2        = {{getHostValue}}:i(strcat(SChannelPrefix, "_wave_2"))
    kHost_pulseWidth_2  = {{getHostValue}}:k(strcat(SChannelPrefix, "_pulseWidth_2"))
    kHost_semi_2        = {{getHostValue}}:k(strcat(SChannelPrefix, "_semi_2"))
    kHost_fine_2        = {{getHostValue}}:k(strcat(SChannelPrefix, "_fine_2"))

    kHost_sub_enabled   = {{getHostValue}}:k(strcat(SChannelPrefix, "_sub_enabled"))
    iHost_sub_wave      = {{getHostValue}}:i(strcat(SChannelPrefix, "_sub_wave"))
    kHost_sub_amp       = {{getHostValue}}:k(strcat(SChannelPrefix, "_sub_amp"))

    kNoteNumber init notnum()

    if (kHost_enabled_1 == {{true}}) then
        kNoteNumber_1 = kNoteNumber + kHost_fine_1 / 100
        aOut += _source_01_module_audio_generator(kHost_mix, cpsmidinn(kNoteNumber_1), iHost_wave_1, kHost_pulseWidth_1)
        ; {{LogDebug_k '("kHost_mix = %f", kHost_mix)'}}
        ; {{LogDebug_k '("kNoteNumber_1 = %f", kNoteNumber_1)'}}
        ; {{LogDebug_i '("iHost_wave_1 = %f", iHost_wave_1)'}}
        ; {{LogDebug_k '("kHost_pulseWidth_1 = %f", kHost_pulseWidth_1)'}}
    endif

    if (kHost_enabled_2 == {{true}}) then
        kNoteNumber_2 = kNoteNumber + kHost_semi_2 + kHost_fine_2 / 100
        aOut += _source_01_module_audio_generator(k(1) - kHost_mix, cpsmidinn(kNoteNumber_2), iHost_wave_2, kHost_pulseWidth_2)
        ; {{LogDebug_k '("kHost_mix = %f", kHost_mix)'}}
        ; {{LogDebug_k '("kNoteNumber_2 = %f", kNoteNumber_2)'}}
        ; {{LogDebug_i '("iHost_wave_2 = %f", iHost_wave_2)'}}
        ; {{LogDebug_k '("kHost_pulseWidth_2 = %f", kHost_pulseWidth_2)'}}
    endif

    if (kHost_sub_enabled == {{true}}) then
        if (iHost_sub_wave == {{source_01.sub.wave.Pulse}}) then
            iMode_sub = {{vco2.mode.SquareWaveNoPWM}}
        elseif (iHost_sub_wave == {{source_01.sub.wave.Triangle}}) then
            iMode_sub = {{vco2.mode.TriangleNoRamp}}
        endif
        aOut += vco2(kHost_sub_amp, cpsmidinn(kNoteNumber - 12), iMode_sub)
        ; {{LogDebug_i '("iMode_sub = %f", iMode_sub)'}}
    endif

end:
    xout(aOut * kHost_main_amp * 0.1)
endop


gi_source_01_module_instance_count init 0


/// Always-on instrument for the `source_01` module.
/// @param 4 Channel prefix used for the host automation parameters. Should match the channel prefix used for the `AF_source_01_module` opcode.
///
instr AF_source_01_module
    SChannelPrefix = p4

    if (frac(p1) == 0) then
        // Retrigger this instrument with a fractional instrument number so it doesn't get turned off if another
        // instance of this instrument is started with a non-fractional instrument number.
        gi_source_01_module_instance_count += 1
        {{LogTrace_i '("Retriggering instrument %d with channel prefix \"%s\" ...", p1, SChannelPrefix)'}}
        {{LogDebug_i '("i%d.%d 0 -1 \"%s\"", p1, gi_source_01_module_instance_count, SChannelPrefix)'}}
        scoreline_i(sprintf("i%d.%d 0 -1 \"%s\"", p1, gi_source_01_module_instance_count, SChannelPrefix))
        turnoff()
    endif

    ; {{LogTrace_k '("Running %d with channel prefix \"%s\" ...", p1, SChannelPrefix)'}}

    kLast_wave_1        init 0
    kLast_wave_2        init 0
    kLast_pulseWidth_1  init 0
    kLast_pulseWidth_2  init 0
    kLast_fine_1        init 0
    kLast_fine_2        init 0

    if ({{getHostValue}}:k(strcat(SChannelPrefix, "_link")) == {{true}}) then
        kHost_wave_1        = {{getHostValue}}:k(strcat(SChannelPrefix, "_wave_1"))
        kHost_wave_2        = {{getHostValue}}:k(strcat(SChannelPrefix, "_wave_2"))
        kHost_pulseWidth_1  = {{getHostValue}}:k(strcat(SChannelPrefix, "_pulseWidth_1"))
        kHost_pulseWidth_2  = {{getHostValue}}:k(strcat(SChannelPrefix, "_pulseWidth_2"))
        kHost_fine_1        = {{getHostValue}}:k(strcat(SChannelPrefix, "_fine_1"))
        kHost_fine_2        = {{getHostValue}}:k(strcat(SChannelPrefix, "_fine_2"))

        if (kHost_wave_1 != kLast_wave_1) then
            kHost_wave_2 = kHost_wave_1
            {{setHostValue}}(strcat(SChannelPrefix, "_wave_2"), kHost_wave_2)
        elseif (kHost_wave_2 != kLast_wave_2) then
            kHost_wave_1 = kHost_wave_2
            {{setHostValue}}(strcat(SChannelPrefix, "_wave_1"), kHost_wave_1)
        endif

        if (kHost_pulseWidth_1 != kLast_pulseWidth_1) then
            kHost_pulseWidth_2 = kHost_pulseWidth_1
            {{setHostValue}}(strcat(SChannelPrefix, "_pulseWidth_2"), kHost_pulseWidth_2)
        elseif (kHost_pulseWidth_2 != kLast_pulseWidth_2) then
            kHost_pulseWidth_1 = kHost_pulseWidth_2
            {{setHostValue}}(strcat(SChannelPrefix, "_pulseWidth_1"), kHost_pulseWidth_1)
        endif

        if (kHost_fine_1 != kLast_fine_1) then
            kHost_fine_2 = -kHost_fine_1
            {{setHostValue}}(strcat(SChannelPrefix, "_fine_2"), kHost_fine_2)
        elseif (kHost_fine_2 != kLast_fine_2) then
            kHost_fine_1 = -kHost_fine_2
            {{setHostValue}}(strcat(SChannelPrefix, "_fine_1"), kHost_fine_1)
        endif

        kLast_wave_1 = kHost_wave_1
        kLast_wave_2 = kHost_wave_2
        kLast_pulseWidth_1 = kHost_pulseWidth_1
        kLast_pulseWidth_2 = kHost_pulseWidth_2
        kLast_fine_1 = kHost_fine_1
        kLast_fine_2 = kHost_fine_2
    endif
endin
